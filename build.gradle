/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.4.0-SNAPSHOT")
        version_qualifier = System.getProperty("build.version_qualifier", "")
        buildVersionQualifier = System.getProperty("build.version_qualifier", "")
        isSnapshot = "true" == System.getProperty("build.snapshot", "true")
        version_tokens = opensearch_version.tokenize('-')
        opensearch_build = version_tokens[0] + '.0'
        plugin_no_snapshot = opensearch_build
        if (buildVersionQualifier) {
            opensearch_build += "-${buildVersionQualifier}"
            plugin_no_snapshot += "-${buildVersionQualifier}"
        }
        if (isSnapshot) {
            opensearch_build += "-SNAPSHOT"
        }
        opensearch_group = "org.opensearch"
        opensearch_no_snapshot = opensearch_build.replace("-SNAPSHOT","")
    }
    repositories {
        mavenLocal()
        maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "${opensearch_group}.gradle:build-tools:${opensearch_version}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:8.2.0"
    }
}

plugins {
    id 'com.avast.gradle.docker-compose' version '0.17.20'
}

// Shared dependency versions across subprojects
ext {
    commonVersions = [
        'jackson': '2.18.2',
        'jetcd': '0.8.6',
        'mockito': '5.20.0',
        'lombok': '1.18.40'
    ]
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

// Configure the plugin subproject with OpenSearch-specific settings
configure(project(':plugin')) {
    apply plugin: 'opensearch.opensearchplugin'
    apply plugin: 'opensearch.java-agent'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'opensearch.pluginzip'
    apply from: rootProject.file('gradle/formatting.gradle')
    
    version = opensearch_version.tokenize('-')[0] + '.0'
    if (version_qualifier) {
        version += "-${version_qualifier}"
    }
    if (isSnapshot) {
        version += "-SNAPSHOT"
    }
}

// Ensure controller subproject doesn't get OpenSearch build tools applied
configure(project(':controller')) {
    // Controller is independent of OpenSearch build tools
}

